O professor pede para leres o texto que escreveste em voz alta para a turma. #portrait:narration #animation:Child_serious #animation:Teacher_smiling #audio:narrator_lvl5_01
Quando olhas à tua volta, sentes o rosto a aquecer... #animation:Child_ashamed #audio:narrator_lvl5_02

-> minigame_puzzle_vergonha

=== continue1 ===
Parabéns, reconheceste a vergonha! A vergonha aparece quando sentimos que os outros estão a reparar em nós de forma negativa ou que nos vão julgar. #portrait:narration #animation:Teacher_smiling #sticker:Vergonha #show:Vergonha #audio:narrator_lvl5_03

Podes começar a ler quando estiveres pronto. #portrait:speech_right_lvl5 #hide:Vergonha #audio:teacher_lvl5_01

-> main

=== main ===
Estão todos a olhar para mim...  E agora? #portrait:thought_lvl5 #animation:Child_ashamed #audio:child_lvl5_01

+ [Hoje não quero ler! $audio:child_lvl5_02]

    Hoje não quero ler! #portrait:speech_left_lvl5_child #animation:Child_stubborn #animation:Teacher_sad #audio:child_lvl5_02
    
    Eu sei que pode parecer mais fácil não fazer isso agora, mas o que custa é o primeiro passo. #portrait:speech_right_lvl5 #animation:Child_veryAshamed #animation:Teacher_serious #audio:teacher_lvl5_02
    
    Depois perdes a vergonha, e vais ver que não é tão difícil quanto pensas. #animation:Teacher_smiling #audio:teacher_lvl5_03
    
    Vamos dar um passo de cada vez, sim? #audio:teacher_lvl5_04
    
    -> main
    
+ [Se eu me enganar vão todos gozar comigo, vai ser terrível. $audio:child_lvl5_03]

    Se eu me enganar vão todos gozar comigo, vai ser terrível. #portrait:speech_left_lvl5_child #animation:Child_veryAshamed #animation:Teacher_sad #audio:child_lvl5_03
    
    Pensa na vergonha como uma bola de neve. #portrait:speech_right_lvl5 #animation:Teacher_smiling #audio:teacher_lvl5_05
    
    Quanto mais pensares no que pode correr mal, mais a bola cresce. #animation:Teacher_smiling #audio:teacher_lvl5_06
    
    É melhor parar, respirar e tentar com calma para desfazer a bola de neve. #animation:Teacher_smiling #audio:teacher_lvl5_07
    
    -> main
    
+ [Se eu me enganar, não faz mal, os meus colegas também se enganam. $audio:child_lvl5_04]

    Se eu me enganar, não faz mal, os meus colegas também se enganam. #portrait:speech_left_lvl5_child #animation:Child_smilingSide #animation:Teacher_veryHappy #audio:child_lvl5_04
    
    Isso mesmo! Imagina que estás a aprender a andar de bicicleta. #portrait:speech_right_lvl5 #animation:Child_smiling #animation:Teacher_smiling #audio:teacher_lvl5_08
    
    No início é normal cair, mas é assim que se aprende a pedalar sem cair. #audio:teacher_lvl5_09
    
    Todos nós erramos, mas não precisas de te esconder! #audio:teacher_lvl5_10
    
    -> minigame_estrelas_da_confianca
    

=== continue2 ===
Parabéns! Cada estrela positiva ajudou a tua confiança a crescer. Agora és uma super estrela pronto para mais desafios! #portrait:narration #sticker:SuperEstrela #show:SuperEstrela #audio:narrator_lvl5_04

-> novas_escolhas

=== novas_escolhas ===
Agora com confiança lês o teu texto para a turma. #portrait:narration #animation:Child_happySide #animation:Teacher_smiling #show:Lines #show:FriendsHappy #hide:Friends #hide:SuperEstrela #audio:narrator_lvl5_05

Consegui! Senti-me bem a ler! #portrait:speech_left_lvl5_child #animation:Child_veryHappy #animation:Teacher_veryHappy #hide:Lines #hide:Notebook #audio:child_lvl5_05

Isso é alegria. E os teus colegas também gostaram do teu texto! #portrait:speech_right_lvl5 #animation:Child_happy #animation:Teacher_smiling #audio:teacher_lvl5_11

Hum... O que devo fazer? #portrait:thought_lvl5 #audio:child_lvl5_06

+ [Vou tentar não sorrir para não acharem que sou convencido. $audio:child_lvl5_07]
    
    Vou tentar não sorrir para não acharem que sou convencido. #portrait:speech_left_lvl5_child #animation:Child_serious #animation:Teacher_sad #audio:child_lvl5_07
    
    Quando mostramos aos outros como estamos felizes, estamos a partilhar algo bom. #portrait:speech_right_lvl5 #animation:Teacher_smiling #audio:teacher_lvl5_12
    
    Não precisas de esconder um sorriso. É natural e bonito de se ver! #audio:teacher_lvl5_13
    -> novas_escolhas

+ [Eu gostei e quero fazer mais vezes! $audio:child_lvl5_08]

    Eu gostei e quero fazer mais vezes! #portrait:speech_left_lvl5_child #animation:Child_veryHappy #animation:Teacher_veryHappy #audio:child_lvl5_08
    
    Ainda bem! A alegria cresce quando repetimos algo bom. #portrait:speech_right_lvl5 #animation:Child_happy #animation:Teacher_smiling #audio:teacher_lvl5_14

    -> continue4
    
=== continue4 ===

Lembras-te do que sentiste no início? Agora terminaste com um sorriso! A vergonha foi embora e a alegria ficou. #portrait:narration #animation:Child_happy #animation:Teacher_smiling #sticker:SorrisoBrilhante #show:SorrisoBrilhante #audio:narrator_lvl5_06

-> END

=== minigame_puzzle_vergonha ===
Qual é a emoção que o João está a sentir? #portrait:narration #minigame:PuzzleVergonha
-> continue1

=== minigame_estrelas_da_confianca ===
Minijogo Estrelas da Confiança #minigame:EstrelasdaConfianca
-> continue2
