Estás na sala com a tua família e o teu irmão acabou de ganhar um presente novo. #portrait:narration #animation:Brother_smilingSide #animation:Mother_smilingSide #audio:narrator_lvl3_01
Ele está super feliz. Mas tu sentes um nó na barriga e fechas as mãos com força... #animation:Child_jealousSide #animation:Brother_happySide #animation:Mother_smilingSide #audio:narrator_lvl3_02

Eu também queria... #portrait:speech_left #audio:child_lvl3_01

-> minigame_puzzle_ciume

=== continue1 ===
Parabéns, reconheceste o ciúme! O ciúme aparece quando sentimos que alguém tem algo que nós também queríamos, como um presente, atenção ou carinho. #portrait:narration #animation:Brother_smilingSide #animation:Mother_smilingSide #hide:Gift #sticker:Ciume #show:Ciume #audio:narrator_lvl3_03

-> main

=== main ===
Porque é que ele ganhou um presente e eu não? #portrait:thought #animation:Child_jealousSide #animation:Brother_smilingSide #animation:Mother_smilingSide #hide:Gift #hide:Ciume #audio:child_lvl3_02

+ [Vocês gostam mais dele do que de mim, ele é o preferido! $audio:child_lvl3_03]

    Vocês gostam mais dele do que de mim, ele é o preferido! #portrait:speech_left #animation:Child_jealous #animation:Brother_serious #animation:Mother_sad #audio:child_lvl3_03
    
    Porque dizes isso? Vou-te contar um segredo. #portrait:speech_right #animation:Mother_sad #audio:mother_lvl3_01
    
    Os pais gostam de todos os filhos da mesma forma e não há preferidos. #audio:mother_lvl3_02
    
    Hoje o teu irmão teve um presente porque estudou e teve uma boa nota no teste. #animation:Mother_sad #audio:mother_lvl3_03
    
    Quando fores para a escola como o teu irmão e tiveres boas notas, também te podemos dar presentes. #animation:Mother_smiling #animation:Brother_smiling #audio:mother_lvl3_04
    -> main
    
+ [Hoje o presente foi para o meu irmão, porque estudou muito e teve boa nota. $audio:child_lvl3_05]

    Hoje o presente foi para o meu irmão, porque estudou muito e teve boa nota. #portrait:speech_left #animation:Child_smiling #animation:Mother_smiling #animation:Brother_smiling #audio:child_lvl3_05
    
    Mas eu também recebo coisas noutras alturas. #audio:child_lvl3_06
    
    Isso mesmo! Cada um pode ter o seu momento e receber presentes. #portrait:speech_right #animation:Mother_happy #audio:mother_lvl3_08
    
    -> novas_escolhas
    
+ [A sério? O meu irmão tem um presente e eu não? Vou fingir que não me importo, não vou mostrar ciúmes. $audio:child_lvl3_04]

    A sério? O meu irmão tem um presente e eu não? Vou fingir que não me importo, não vou mostrar ciúmes. #portrait:speech_left #animation:Child_stubborn #animation:Brother_serious #animation:Mother_sad #audio:child_lvl3_04
    
    Queres falar comigo sobre o presente do teu irmão? Acho que ficaste com um bocadinho de ciúmes. #portrait:speech_right #animation:Mother_sad #audio:mother_lvl3_05
    
    Ficaste? Não faz mal. Às vezes sentimos isso quando alguém tem o que nós queríamos. #audio:mother_lvl3_06
    
    Mas devemos mostrar aos outros como nos sentimos para eles nos ajudarem a ficarmos mais felizes, não achas? #animation:Mother_smiling #animation:Brother_smiling #audio:mother_lvl3_07
    -> main
    
=== novas_escolhas ===
O importante é ambos estarem felizes, não achas? #audio:mother_lvl3_09

Hum... #portrait:thought #audio:child_lvl3_07

+ [Acho que se ele ficar feliz também fico feliz por ele. $audio:child_lvl3_08]

    Acho que se ele ficar feliz também fico feliz por ele. #portrait:speech_left #audio:child_lvl3_08
    
    Isso é muito bonito da tua parte! #portrait:speech_right #audio:mother_lvl3_10
    Ao apoiares o teu irmão, estás a crescer e partilhar alegria. #audio:mother_lvl3_11
    
    -> minigame_caixa_dos_sorrisos
    
+ [Ainda tenho um bocadinho de ciúmes, mas já consigo entender. $audio:child_lvl3_09]

    Ainda tenho um bocadinho de ciúmes, mas já consigo entender. #portrait:speech_left #audio:child_lvl3_09
    
    É normal ainda te sentires assim, mas estás no caminho certo! #portrait:speech_right #audio:mother_lvl3_12
    Lembra-te de que também podes sentir alegria ao ver os outros felizes. #audio:mother_lvl3_13
    
    -> minigame_caixa_dos_sorrisos


=== continue2 ===
Parabéns! Conseguiste encontrar muitos sorrisos ao teu redor! Agora a tua caixa está cheia de alegria para partilhares com quem mais gostas. #portrait:narration #animation:Mother_veryHappy #animation:Brother_happy #sticker:CaixaDaAlegria #show:CaixaDaAlegria #audio:narrator_lvl3_04

Agora já sabes, quando sentires ciúmes outra vez lembra-te: #portrait:speech_right #animation:Mother_smiling #animation:Brother_smiling #hide:CaixaDaAlegria #audio:mother_lvl3_14

É normal querer o que os outros têm, mas também é bonito aprender a ficar feliz por eles. #audio:mother_lvl3_15

O teu coração ficou apertado com ciúmes, mas conseguiste abri-lo para entender os sentimentos dos outros. De cada vez que fazes isso, o teu coração cresce! #portrait:narration #animation:Mother_veryHappy #sticker:CoracaoGrande #show:CoracaoGrande #audio:narrator_lvl3_05

-> END

=== minigame_puzzle_ciume ===
Qual é a emoção que o João está a sentir? #portrait:narration #minigame:PuzzleCiume
-> continue1

=== minigame_caixa_dos_sorrisos ===
Minijogo Caixa dos Sorrisos #minigame:CaixaDosSorrisos
-> continue2
