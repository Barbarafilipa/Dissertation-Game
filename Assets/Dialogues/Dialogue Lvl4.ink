Hoje é o dia do teste de matemática. #portrait:narration #audio:narrator_lvl4_01
Sentes o teu coração a bater mais rápido, as mãos a suar e um nó na barriga... #animation:Child_anxious #show:Heart #audio:narrator_lvl4_02

-> minigame_puzzle_ansiedade

=== continue1 ===
Parabéns, reconheceste a ansiedade! A ansiedade aparece quando temos medo do que pode acontecer no futuro e não sabemos exatamente como vai ser. #portrait:narration #animation:Child_anxious #hide:Heart #sticker:Ansiedade #show:Ansiedade #audio:narrator_lvl4_03

Ei, estás pronto para o teste? #portrait:speech_right_lvl4_friend #speaker:Friend #animation:Child_serious #animation:Friend_happy #hide:Ansiedade #audio:friend_lvl4_01

-> main

=== main ===
E se eu errar tudo? #portrait:thought #animation:Child_anxious #animation:Friend_serious #hide:Heart

+ [Não, vou dizer à professora que estou doente para não fazer.]

    Não, vou dizer à professora que estou doente para não fazer. #portrait:speech_left #animation:Friend_serious
    
    Mesmo que digas que estás doente, um dia vais ter que fazer o teste e a ansiedade vai ser maior ainda. #portrait:speech_right_lvl4_friend #animation:Child_veryAnxious #show:Heart #audio:friend_lvl4_02
    
    Fazer o teste agora pode ser difícil, mas depois não precisas de te preocupar mais com ele. #animation:Friend_smiling #audio:friend_lvl4_03
    
    -> main
    
+ [Estou ansioso, mas vou tentar fazer o teste.]

    Estou ansioso, mas vou tentar fazer o teste. #portrait:speech_left #animation:Child_worried #animation:Friend_veryHappy
    
    Vou te contar um segredo, eu também estou nervoso para o teste. #portrait:speech_right_lvl4_friend #animation:Child_serious #animation:Friend_smiling #audio:friend_lvl4_04
    
    Mas às vezes não podemos evitar que isso aconteça. #audio:friend_lvl4_05
    
    Temos que pensar de forma calma para conseguirmos lidar com a ansiedade. #animation:Child_smiling #audio:friend_lvl4_06
    -> minigame_cano_dos_pensamentos
    
+ [Eu estudei muito, então vai correr bem.]

    Eu estudei muito, então vai correr bem. #portrait:speech_left #animation:Child_smiling #animation:Friend_veryHappy
    
    Isso mesmo, tens que pensar que estudaste muito e que por isso vais saber responder a tudo. #portrait:speech_right_lvl4_friend #animation:Friend_happy #audio:friend_lvl4_07
    
    Um teste é apenas uma avaliação, se não correr bem, depois podes melhorar nos próximos. #animation:Friend_smiling #audio:friend_lvl4_08
    -> minigame_cano_dos_pensamentos

=== continue2 ===
Parabéns! Encontraste o caminho positivo! Quando escolhemos ver as coisas de forma mais calma e positiva, ficamos mais tranquilos, mesmo quando sentimos ansiedade. #portrait:narration #animation:Child_happy #animation:Friend_veryHappy #sticker:PensamentoPositivo #show:PensamentoPositivo #audio:narrator_lvl4_04

Acho que já me sinto mais calmo. #portrait:speech_left #animation:Child_smiling #animation:Friend_smiling #hide:PensamentoPositivo

Agora o teste começa. Olhas para a primeira pergunta e não sabes responder. #portrait:narration #show:Mask #show:PaperTest #hide:Friend #animation:Child_sit #animation:Child_serious #audio:narrator_lvl4_05

-> novas_escolhas

=== novas_escolhas ===
E agora? #portrait:thought_lvl4 #animation:Child_anxious #hide:Heart

+ [Eu devia ter estudado mais! Vou falhar o teste todo!]
    
    Eu devia ter estudado mais! Vou falhar o teste todo! #portrait:speech_left_lvl4_child #animation:Child_veryAnxious #show:Heart
    
    Se continuares a pensar nas coisas negativas, a tua cabeça vai ficar cheia de preocupações. #portrait:speech_right #speaker:Teacher #animation:Teacher_sad #audio:teacher_lvl4_01
    
    Precisas de te focar no que podes fazer para melhorar, para não perderes muito tempo do teste.#animation:Teacher_serious #audio:teacher_lvl4_02
    -> novas_escolhas

+ [Vou avançar esta e tentar outra primeiro.]

    Vou avançar esta e tentar outra primeiro. #portrait:speech_left_lvl4_child #animation:Child_smiling #animation:Teacher_veryHappy
    
    Às vezes mudar o que estamos a fazer ajuda-nos a tornar tudo mais tranquilo. #portrait:speech_right #speaker:Teacher #animation:Teacher_smiling #audio:teacher_lvl4_03
    
    Se uma pergunta é muito difícil, tentar outras mais fáceis é uma forma inteligente de pensar. #audio:teacher_lvl4_04

    -> minigame_ordem_inteligente
    
+ [Vou fechar os olhos e respirar devagar.]

    Vou fechar os olhos e respirar devagar. #portrait:speech_left_lvl4_child #animation:Child_smiling #animation:Teacher_veryHappy
    
    Quando estamos nervosos, o nosso coração bate mais rápido, mas com respirações profundas, conseguimos acalmar esse sentimento. #portrait:speech_right #speaker:Teacher #animation:Teacher_smiling #audio:teacher_lvl4_05
    
    Imagina que tens uma flor na mão e uma vela na outra. #show:Flower #show:Candle #audio:teacher_lvl4_06
    
    Agora cheira a flor bem devagar pelo nariz... #hide:Candle #show:Inspirar #hide:Expirar #audio:teacher_lvl4_07
    
    E agora sopra a vela com calma pela boca... #hide:Flower #show:Candle #animation:Child_happy #animation:Teacher_happy #hide:Inspirar #show:Expirar #audio:teacher_lvl4_08
    
    Outra vez! Cheira a flor... #show:Flower #hide:Candle #animation:Teacher_smiling #show:Inspirar #hide:Expirar #audio:teacher_lvl4_09
    
    Sopra a vela... #show:Candle #hide:Flower #animation:Child_happy #animation:Teacher_happy #hide:Inspirar #show:Expirar #audio:teacher_lvl4_10
    
    Muito bem! Quando respiras assim o teu corpo acalma e a ansiedade começa a ir embora. #animation:Child_veryHappy #hide:Candle #animation:Teacher_veryHappy #hide:Inspirar #hide:Expirar #sticker:RespiracaoTranquila #show:RespiracaoTranquila #audio:teacher_lvl4_11

    -> continue4
    
=== continue3 ===
Parabéns! Ao escolheres começar pelas perguntas mais fáceis conseguiste controlar a ansiedade durante o teste. Foi uma escolha inteligente! #portrait:narration #animation:Child_happy #animation:Teacher_happy #sticker:EscolhaInteligente #show:EscolhaInteligente #audio:narrator_lvl4_06

-> continue4

=== continue4 ===
Boa! Consegui terminar! #portrait:speech_left_lvl4_child #animation:Child_veryHappy #animation:Teacher_veryHappy #hide:RespiracaoTranquila #hide:EscolhaInteligente

Foi difícil, mas conseguiste controlar a ansiedade! #portrait:speech_right #audio:teacher_lvl4_12

Mesmo sentindo ansiedade conseguiste fazer o teste. Ao acalmares o teu corpo e organizares os teus pensamentos consegues passar qualquer desafio! #portrait:narration #animation:Child_happy #animation:Teacher_happy #sticker:DesafioSuperado #show:DesafioSuperado #audio:narrator_lvl4_07

-> END

=== minigame_puzzle_ansiedade ===
Qual é a emoção que o João está a sentir? #portrait:narration #minigame:PuzzleAnsiedade
-> continue1

=== minigame_cano_dos_pensamentos ===
Minijogo Cano dos Pensamentos #minigame:CanoDosPensamentos
-> continue2

=== minigame_ordem_inteligente ===
Minijogo Ordem Inteligente #minigame:OrdemInteligente
-> continue3
